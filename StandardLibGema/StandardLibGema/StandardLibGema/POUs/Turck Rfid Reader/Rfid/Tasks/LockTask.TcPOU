<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.12">
  <POU Name="LockTask" Id="{bf1201d9-e6ea-4b53-8d29-c01388a61fd3}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK LockTask EXTENDS TcoCore.TcoTask
VAR_IN_OUT
    rfidInOut : RfidInOut;
END_VAR
VAR_INPUT
    ActionDesc : STRING(25);
END_VAR
VAR
    _stringBuilder : TcoCore.StringBuilder;
    _state         : INT;

    _eErrorID   : RFID_FB_U.ERROR;
    _data       : Tc2_System.T_MaxString;
    _noTagFound : BOOL;

    _offSet : UDINT;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF (THIS^.Execute()) THEN
    THIS^.ThrowWhen(rfidInOut.xError);

    CASE _state OF

        10:
            // Tag vorhanden
            IF NOT rfidInOut.xTP THEN
                THIS^.Messenger.Error
                (
                    _stringBuilder.Append(ActionDesc)
                        .Append('Tag nicht vorhanden bei Schreibversuch')
                        .ToString()
                );

                _noTagFound := true;
                ThrowWhen(TRUE);
            ELSE
                _state := 20;
            END_IF
        20:
            //Command setzen
            rfidInOut.eCommand := RFID_FB_U.COMMAND.SET_PERMA_LOCK;
            ADSLOGSTR
            (
                msgCtrlMask := ADSLOG_MSGTYPE_ERROR
                , msgFmtStr := 'Startadresse bei CMD: %s'
                , strArg    := TO_STRING(rfidInOut.udiStartAdress)
            );

            _state := 30;
        30:
            // Start der Operation
            rfidInOut.xExecute := TRUE;

            _state := 40;
        40:
            // Warte auf Ende Operation
            IF rfidInOut.xError THEN
                ;
            ELSIF rfidInOut.xDone THEN

                THIS^.DoneWhen(rfidInOut.xDone);
                //                THIS^.Messenger.Info
                //                (
                //                    _stringbuilder.Append('Lock der Daten erfolgreich')
                //                        .Append('Startadresse: ')
                //                        .Append(TO_STRING(rfidinOut.udiStartAdress))
                //                        .Append(' Laenge: ')
                //                        .Append(TO_STRING(rfidInOut.uiDataLenght))
                //                        .ToString()
                //                );
                ADSLOGSTR
                (
                    msgCtrlMask := ADSLOG_MSGTYPE_ERROR
                    , msgFmtStr := 'Startadresse nach Done: %s'
                    , strArg    := TO_STRING(rfidInOut.udiStartAdress)
                );

                _state := 0;
            END_IF
    END_CASE
END_IF
]]></ST>
    </Implementation>
    <Folder Name="Status" Id="{c7b295a0-e312-4e99-a7b7-ac922da557cc}" />
    <Folder Name="TcOpen" Id="{373b465a-833c-46e9-9643-4b79a9b55ab8}" />
    <Property Name="ErrorId" Id="{25db9957-9637-40aa-aa65-3c086df9908c}" FolderPath="Status\">
      <Declaration><![CDATA[{attribute 'monitoring' := 'call'}
PROPERTY ErrorId : RFID_FB_U.ERROR]]></Declaration>
      <Get Name="Get" Id="{7e7d3347-7b26-481a-a973-7129a670668f}">
        <Declaration><![CDATA[VAR
END_VAR
]]></Declaration>
        <Implementation>
          <ST><![CDATA[{warning disable C0371}
ErrorId := _eErrorID;]]></ST>
        </Implementation>
      </Get>
    </Property>
    <Method Name="FB_init" Id="{da16ec47-5954-4bd1-a9c8-4c6c1ba99c0b}">
      <Declaration><![CDATA[METHOD PUBLIC FB_init : BOOL
VAR_INPUT
    bInitRetains : BOOL; // if TRUE, the retain variables are initialized (warm start / cold start)
    bInCopyCode  : BOOL; // if TRUE, the instance afterwards gets moved into the copy code (online change)
    ParentObject : ITcoObject;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// This is not clear to me... I from time to time the consumer code does not see 'FB_init' of 'TcoObject', that's why overridden here with super call...
{warning disable C0316}
SUPER^.FB_init(bInitRetains, bInCopyCode, ParentObject);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnAbort" Id="{b1d74c16-773b-40ba-8a32-93ed5d11cb8b}" FolderPath="TcOpen\">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task is aborted.
        </summary>
    </docu>
~*)
METHOD PROTECTED OnAbort
]]></Declaration>
      <Implementation>
        <ST><![CDATA[{warning disable C0371}
RfidInOut.eCommand := RFID_FB_U.COMMAND.IDLE;
RfidInOut.xExecute := FALSE;
RfidInOut.xAbort   := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnDone" Id="{c0d99c20-e6a3-4a0a-ba89-8364e1c2a7d3}" FolderPath="TcOpen\">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task reaches the ```Done``` state .
        </summary>
    </docu>
~*)
METHOD PROTECTED OnDone
]]></Declaration>
      <Implementation>
        <ST><![CDATA[{warning disable C0371}
rfidInOut.xExecute := FALSE;
rfidInOut.eCommand := RFID_FB_U.COMMAND.IDLE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnError" Id="{6216c8f2-4440-4ef0-9c41-32e9879462be}" FolderPath="TcOpen\">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes when task reaches the ```Error``` state.
            <para>
                Do not confuse with <see cref="PlcDocu.TcoCore.TcoTask.WhileError()"/>, that is called cyclically until '''Error''' state is left.
            </para>
        </summary>
    </docu>
~*)
METHOD PROTECTED OnError
]]></Declaration>
      <Implementation>
        <ST><![CDATA[{warning disable C0371}
IF _noTagFound THEN
    _eErrorID := RFID_FB_U.ERROR.COMMAND_NO_TAG_FOUND;
    // not implemented by Turck
ELSIF rfidInOut.eErrorID = 16#2901 THEN
    _eErrorID := RFID_FB_U.ERROR.COMMAND_ADDRESS_OUTSIDE_BLOCK_BOUNDARIES;
ELSE
    _eErrorID := rfidInOut.eErrorID;
END_IF

THIS^.Messenger.Build().Append('Rfid Schreib/Lesekopf Fehler : ').Append(to_string(THIS^._eErrorID)).As().AsError();]]></ST>
      </Implementation>
    </Method>
    <Method Name="OnStart" Id="{6c97fc65-8bc5-417e-8894-b1e0708b0590}" FolderPath="TcOpen\">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Executes ones when the task starts (at the moment of transition from the ```Requested``` state into the ```Busy``` state).
        </summary>
    </docu>
~*)
METHOD PROTECTED OnStart
]]></Declaration>
      <Implementation>
        <ST><![CDATA[{warning disable C0371}
rfidInOut.eCommand := RFID_FB_U.COMMAND.IDLE;
rfidInOut.xExecute := FALSE;
rfidInOut.xAbort   := FALSE;
_state             := 10;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Restore" Id="{790ee270-a250-40b8-a71b-084eeeecc8d6}" FolderPath="TcOpen\">
      <Declaration><![CDATA[(*~
    <docu>
        <summary>
            Restores this instance to the Ready state. Can be called explicitly or from within one of the restore mechanisms.
            <para>
                See also <see cref="PlcDocu.TcoCore.TcoTask.AutoRestorable()"/>
            </para>
        </summary>
    </docu>
~*)
METHOD Restore : ITcoRestorable
]]></Declaration>
      <Implementation>
        <ST><![CDATA[{warning disable C0371}
RfidInOut.eCommand := RFID_FB_U.COMMAND.IDLE;
RfidInOut.xExecute := FALSE;
RfidInOut.xAbort   := FALSE;
_noTagFound        := FALSE;
_eErrorID          := RFID_FB_U.ERROR.NO_ERROR;
SUPER^.Restore();]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>